
#Область ПрограммныйИнтерфейс

#Область ПроверкаКандидатаСлужбойБезопасности

// Функция - Документ проверки кандидата СБПо документу основанию
//
// Параметры:
//  ДокументОснование	 - ДокументСсылка.ПодборКандидатаНаВакансию - Документ подбор кандидата.
// 
// Возвращаемое значение:
//  ДокументПроверки	 - ДокументСсылка.ПроверкаКандидатаСлужбойБезопасности, Неопределено - Документ проверки.
//
Функция ДокументПроверкиКандидатаСБПоДокументуОснованию(ДокументОснование) Экспорт
	
	ДокументПроверки = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПроверкаКандидатаСлужбойБезопасности.Ссылка КАК Документ
		|ИЗ
		|	Документ.ПроверкаКандидатаСлужбойБезопасности КАК ПроверкаКандидатаСлужбойБезопасности
		|ГДЕ
		|	ПроверкаКандидатаСлужбойБезопасности.ДокументОснование = &ДокументОснование
		|	И НЕ ПроверкаКандидатаСлужбойБезопасности.ПометкаУдаления
		|	И ПроверкаКандидатаСлужбойБезопасности.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПроверкаКандидатаСлужбойБезопасности.Дата УБЫВ";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументПроверки = Выборка.Документ;
	КонецЦикла;
	
	Возврат ДокументПроверки;
	
КонецФункции

// Процедура - Создать документ проверка кандидата СБПо документу основанию
//
// Параметры:
//  ДокументОснование	 - ДокументСсылка.ПодборКандидатаНаВакансию - Документ подбор кандидата.
//
Процедура СоздатьДокументПроверкаКандидатаСБПоДокументуОснованию(ДокументОснование) Экспорт
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ДокументОснование", ДокументОснование);
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДата());
	
	ДокументОбъект = Документы.ПроверкаКандидатаСлужбойБезопасности.СоздатьДокумент();
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти