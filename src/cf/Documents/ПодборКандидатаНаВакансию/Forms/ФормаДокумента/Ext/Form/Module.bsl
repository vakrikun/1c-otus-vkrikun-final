
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
		Объект.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗапроситьПроверкуКандидатаВСБ(Команда)
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ЗапроситьПроверкуКандидатаВСБЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Документ будет записан. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗапроситьПроверкуКандидатаВСБНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапроситьПроверкуКандидатаВСБЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗапроситьПроверкуКандидатаВСБНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗапроситьПроверкуКандидатаВСБНаСервере()
	
	ПодборПерсоналаHRM.СоздатьДокументПроверкаКандидатаСБПоДокументуОснованию(Объект.Ссылка);
	
	ЗаполнитьДанныеДокументаПроверкиСБ();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЗаполнитьДанныеДокументаПроверкиСБ();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДокументаПроверкиСБ()
	
	ДокументПроверкиСБ = ПодборПерсоналаHRM.ДокументПроверкиКандидатаСБПоДокументуОснованию(Объект.Ссылка);
	СостояниеПроверкиСБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументПроверкиСБ, "СостояниеПроверки");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЕстьДокументПроверкиСБ = ЗначениеЗаполнено(Форма.ДокументПроверкиСБ);
	
	Элементы.ДокументПроверкиСБ.Видимость = ЕстьДокументПроверкиСБ;
	Элементы.СостояниеПроверкиСБ.Видимость = ЕстьДокументПроверкиСБ;
	Элементы.ЗапроситьПроверкуСлужбыБезопасности.Видимость = Не ЕстьДокументПроверкиСБ;
	
	Если ЕстьДокументПроверкиСБ Тогда
		Если Форма.СостояниеПроверкиСБ = ПредопределенноеЗначение("Перечисление.СостоянияПроверкиКандидатов.Пройдена") Тогда
			Элементы.ГруппаСостояниеПроверкиСБ.ЦветФона = WebЦвета.СветлоЗеленый;
		ИначеЕсли Форма.СостояниеПроверкиСБ = ПредопределенноеЗначение("Перечисление.СостоянияПроверкиКандидатов.НаПроверке") Тогда
			Элементы.ГруппаСостояниеПроверкиСБ.ЦветФона = WebЦвета.Золотой;
		Иначе
			Элементы.ГруппаСостояниеПроверкиСБ.ЦветФона = WebЦвета.СветлоКоралловый;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
